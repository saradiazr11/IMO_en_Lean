\chapter{Elementos de matemáticas en Lean}\label{cap2}

En este capítulo, para continuar con la introducción al uso de Lean, se
presentan algunas demostraciones de las que se estudian en diversas
asignaturas del Grado de Matemáticas formalizadas en Lean.

\section{Cálculo infinitesimal}

En esta primera sección veremos diferentes resultados de análisis
correspondientes a la asignatura de formación básica Cálculo
Infinitesimal del primer año del Grado.

Para llevar a cabo el desarrollo de tales resultados hemos hecho uso
de \cite{ML} y \cite{LP}.

Estudiaremos un total de seis resultados. Los tres primeros son
relativos a la convergencia de sucesiones, mientras que los tres últimos
son sobre la paridad de las funciones.

\subsection{Unicidad del límite}

\begin{definicion}\label{limite}
  Una sucesión \(\{u_n\}\) se dice que converge a un número real \(c\)
  si, dado cualquier número real \(ε > 0\), existe un número natural
  \(N\) tal que si \(n\) es cualquier número natural mayor o igual que
  \(N\) se cumple que \(|u_n-c| ≤ ε\). Simbólicamente,
  \[∀ ε > 0, ∃ N ∈ ℕ, ∀ n ≥ N, |u_n-c| ≤ ε \]
\end{definicion}

La formalización de la definición anterior en Lean es
\begin{leancode}
def limite : (ℕ → ℝ) → ℝ → Prop :=
λ u c, ∀ ε > 0, ∃ N, ∀ n ≥ N, |u n - c| ≤ ε
\end{leancode}
\index{limite}
donde la notación para el valor absoluto se ha definido por
\begin{leancode}
notation `|`x`|` := abs x
\end{leancode}

\begin{teorema}
Cada sucesión tiene como máximo un límite.
\end{teorema}

\begin{demostracion}
  Comenzaremos viendo la demostración en lenguaje natural. Para ello,
  procederemos por reducción al absurdo: supondremos que la sucesión
  \(\{u_n\}\) posee dos límites distintos que denotaremos por \(l\)
  y \(l'\). Tenemos que demostrar que estos límites son iguales;
  es decir, que \(l=l'\).

  En primer lugar, por la definición de límite anterior, \ref{limite},
  se tiene que para el límite \(l\) podemos escribir que:
  \begin{equation}\label{lim1}
    ∀ ε > 0, ∃ N_1 ∈ ℕ, ∀ n ≥ N_1 |u_n-l| ≤ \frac{ε}{2}.
  \end{equation}
  Análogamente, para \(l'\) se tiene que:
  \begin{equation}\label{lim2}
    ∀ ε > 0, ∃ N_2 ∈ ℕ, ∀ n ≥ N_2 |u_n-l'| ≤ \frac{ε}{2}.
  \end{equation}

  A continuación, definamos \(N_0 = \max(N_1,N_2)\).  De manera que,
  considerando un número arbitrario \(n\) que sea mayor o igual que
  \(N_0\) y teniendo en cuenta lo visto anteriormente, podemos escribir:
  \[\begin{array}{llll}
      |l-l'| &= &|l-u_{N_0}+u_{N_0}-l'|   & \\
             &≤ &|u_{N_0}-l|+|u_{N_0}-l'| & [(*)] \\
             &≤ &\frac{ε}{2}+\frac{ε}{2} & [(**)] \\
             &= &ε
  \end{array}\]
  donde en \((*)\) se ha hecho uso de la desigualdad triangular y la
  definición de valor absoluto y en \((**)\) se ha usado las
  desigualdades (\ref{lim1}) y (\ref{lim2}).

  Por tanto, se ha demostrado que para todo \(ε\) mayor que cero, se
  verifica que
  \begin{equation}\label{leq}
    |l-l'| ≤ ε.
  \end{equation}

  Finalmente, se concluye la demostración sin más que aplicar sobre
  (\ref{leq}) el siguiente resultado:
  \begin{equation}
    ∀ x, y ∈ ℝ, (∀ ε > 0, |x - y| ≤ ε) ⟹ x = y.
  \end{equation}
\end{demostracion}

La formalización en Lean del teorema anterior es
\begin{leancode}
import data.real.basic

variables (u : ℕ → ℝ)
variables (a b x y : ℝ)

notation `|`x`|` := abs x

def limite : (ℕ → ℝ) → ℝ → Prop :=
λ u c, ∀ ε > 0, ∃ N, ∀ n ≥ N, |u n - c| ≤ ε

lemma eq_of_abs_sub_le_all
  : (∀ ε > 0, |x - y| ≤ ε) → x = y :=
begin
  intro h,
  apply eq_of_abs_sub_nonpos,
  by_contradiction H,
  push_neg at H,
  specialize h (|x-y|/2) (by linarith),
  linarith,
end

example
  (ha : limite u a)
  (hb : limite u b)
  : a = b :=
begin
  apply eq_of_abs_sub_le_all,
  intros eps eps_pos,
  cases ha (eps/2) (by linarith) with N1 hN1,
  cases hb (eps/2) (by linarith) with N2 hN2,
  let N0:=max N1 N2,
  calc  |a-b|
      = |(a-u N0)+(u N0-b)| : by ring_nf
  ... ≤ |a-u N0| + |u N0-b| : by apply abs_add
  ... = |u N0-a| + |u N0-b| : by rw abs_sub
  ... ≤ eps                 : by linarith [hN1 N0 (le_max_left N1 N2),
                                           hN2 N0 (le_max_right N1 N2)],
end
\end{leancode}

Para poder trabajar con el conjunto de los números reales ha sido necesario
importar la librería \href{https://github.com/leanprover-community/mathlib/blob/master/src/data/real/basic.lean}
{data.real.basic}.

En la formalización de este teorema Lean han sido necesarios el uso
de los siguiente lemas y teoremas:
\begin{itemize}
\item \mint{lean}|eq_of_abs_sub_nonpos : (a - b) ≤ 0 → a = b|
  \indLema{eq\_of\_abs\_sub\_nonpos}
\item \mint{lean}|abs_add : abs (a + b) ≤ abs a + abs b|
  \indLema{abs\_add}
\item \mint{lean}|abs_sub : abs (a - b) = abs (b - a)|
  \indLema{abs\_sub}
\item \mint{lean}|le_max_left : a ≤ max a b|
  \indLema{le\_max\_left}
\item \mint{lean}|le_max_right : b ≤ max a b|
  \indLema{le\_max\_right}
\end{itemize}

Además, también se han usado las tácticas
\tactica{apply}{apply},
\tactica{by_contradiction}{by\_contradiction},
\tactica{push_neg}{push\_neg},
\tactica{specialize}{specialize},
\tactica{calc}{calc},
\tactica{intro / intros}{intro},
\tactica{intro / intros}{intros},
\tactica{cases}{cases} y
\tactica{linarith}{linarith}.

\subsection{Las sucesiones convergentes son sucesiones de Cauchy}

Previo al enunciado y demostración del teorema, veamos en qué consiste
una sucesión de Cauchy.

\begin{definicion}
  Una sucesión \(\{u_n\}\) se dice que es de Cauchy si, dado cualquier
  número real \(ε > 0\), existe un número natural \(N\) tal que si \(p\)
  y \(q\) son cualesquiera dos números naturales mayores o iguales que
  \(N\) se cumple que \(|u_p - u_q| ≤ ε\). Simbólicamente:
  \begin{equation}
  ∀ ε > 0, N ∈ ℕ, ∀ p, q ≥ N, |u_p - u_q| ≤ ε.
  \end{equation}
\end{definicion}

En Lean esta definición se formaliza como:
\begin{leancode}
def cauchy_sequence (u : ℕ → ℝ) :=
∀ ε > 0, ∃ N, ∀ p q, p ≥ N → q ≥ N → |u p - u q| ≤ ε
\end{leancode}

\begin{teorema}
  Toda sucesión convergente es una sucesión de Cauchy.
\end{teorema}
\begin{demostracion}
  Sea \(\{u_n\}\) una sucesión convergente; es decir, por la definición
  \ref{limite}, se tiene que si denotamos \(l\) como el límite de la
  sucesión en cuestión se verifica que

  \begin{equation}\label{lim3}
  ∀ ε > 0, ∃ N ∈ ℕ, ∀ n ≥ N |u_n-l| ≤ \frac{ε}{2}.
  \end{equation}

  A continuación, consideremos \(p,q ∈ ℕ\) tales que estos dos números
  verifican que son mayores o iguales que \(N\). Entonces tenemos que se
  puede escribir:

  \[\begin{array}{llll}
      |u_p-u_q| &= &|u_p-l+l-u_q|           & \\
                &≤ &|u_p-l|+|u_q-l|         & [(*)] \\
                &≤ &\frac{ε}{2}+\frac{ε}{2} & [(**)] \\
                &= &ε
  \end{array}\]
  donde \((*)\) se ha hecho uso de la desigualdad triangular y de la
  propia definición del valor absoluto; mientras que en \((**)\) se ha
  usado lo visto en (\ref{lim3}) para los casos de \(p\) y \(q\).

  De esta forma, se ha demostrado que para cualquier número real \(ε > 0\)
  se verifica que \(|u_p - u_q| ≤ 0\); es decir, hemos demostrado
  que \(\{u_n\}\) es una sucesión de Cauchy.
\end{demostracion}

En Lean esto se formaliza como sigue:
\begin{leancode}
import data.real.basic

variables {u : ℕ → ℝ} {a l : ℝ}

notation `|`x`|` := abs x

def seq_limit (u : ℕ → ℝ) (l : ℝ) : Prop :=
∀ ε > 0, ∃ N, ∀ n ≥ N, |u n - l| ≤ ε

def cauchy_sequence (u : ℕ → ℝ) :=
∀ ε > 0, ∃ N, ∀ p q, p ≥ N → q ≥ N → |u p - u q| ≤ ε

example : (∃ l, seq_limit u l) → cauchy_sequence u :=
begin
  intros h eps eps_pos,
  cases h with l hl,
  rw seq_limit at hl,
  cases hl (eps/2) (by linarith) with N hN,
  use N,
    intros p q hp hq,
  calc  |u p - u q|
      = |(u p - l)+(l - u q)|  : by ring
  ... ≤ |u p - l| + |l - u q|  : by apply abs_add
  ... = |u p - l| + |u q - l|  : by rw abs_sub l (u q)
  ... ≤ eps/2 + eps/2          : by linarith [hN p hp, hN q hq]
  ... = eps                    : by ring,
end
\end{leancode}

Al igual que en el resultado anterior, para la formalización que
acabamos de plantear, ha sido necesario importar la librería
\href{https://github.com/leanprover-community/mathlib/blob/master/src/data/real/basic.lean}
{data.real.basic} para poder trabajar con los números reales y se han
usado los dos siguientes lemas auxiliares:
\begin{itemize}
\item \mint{lean}|abs_add : abs (a + b) ≤ abs a + abs b|
  \indLema{abs\_add}
\item \mint{lean}|abs_sub : abs (a - b) = abs (b - a)|
  \indLema{abs\_sub}
\end{itemize}
Además,se han usado las tácticas
\tactica{rw /rewrite}{rw}.
\tactica{use}{use},
\tactica{apply}{apply},
\tactica{ring}{ring},
\tactica{intro / intros}{intros},
\tactica{cases}{cases} y
\tactica{linarith}{linarith}.

\subsection{Suma de sucesiones convergentes}

\begin{teorema}
  Sean \(\{u_n\}\) y \(\{v_n\}\) dos sucesiones convergentes, entonces
  se tiene que la suma de estas dos sucesiones converge a la suma de sus
  respectivos límites. Simbólicamente,
  \begin{equation}
  lim (u_n + v_n) = lim (u_n) + lim (v_n).
  \end{equation}
\end{teorema}

\begin{demostracion}
  Como consecuencia de que las sucesiones \(\{u_n\}\) y \(\{v_n\}\) sean
  convergentes se tiene que:
  \begin{align}
    & ∀ ε > 0, ∃ N_1 ∈ ℕ, ∀ n ≥ N_1 |u_n - l| ≤ \frac{ε}{2} \label{limU} \\
    & ∀ ε > 0, ∃ N_2 ∈ ℕ, ∀ n ≥ N_1 |v_n - l'| ≤ \frac{ε}{2}, \label{limV}
  \end{align}
  donde hemos denotado \(l\) y \(l'\) a los límites de \(\{u_n\}\) y
  \(\{v_n\}\), respectivamente.

  A continuación, definimos \(N_0 := \max(N_1,N_2)\) y consideremos un
  número natural \(n\) que sea mayor o igual que \(N_0\), de manera que:
  \[\begin{array}{llll}
      |(u_n+v_n)-(l+l')| &= &|u_p-l+l-u_q|           & \\
                         &≤ &|u_n-l|+|v_n-l'|        & [(*)] \\
                         &≤ &\frac{ε}{2}+\frac{ε}{2} & [(**)] \\
                         &= &ε
  \end{array}\]
  donde en \((*)\) se usado la desigualdad triangular y en \((**)\) las
  desigualdades vistas en (\ref{limU}) y (\ref{limV}).

  De manera que finalmente se ha demostrado que para cualquier número
  real \(ε > 0\), existe un número natural \(N_0\) tal que para
  cualquier número \(n\) mayor o igual que \(N_0\), se verifica que
  \(|(u_n+v_n)-(l+l')| ≤ ε\), o lo que es lo mismo que la sucesión
  \(\{u_n+v_n\}\) converge a \((l+l')\).
\end{demostracion}

En Lean esto se formaliza como:
\begin{leancode}
import data.real.basic

variables {u v: ℕ → ℝ} {l l' : ℝ}

notation `|`x`|` := abs x

def seq_limit (u : ℕ → ℝ) (l : ℝ) : Prop :=
∀ ε > 0, ∃ N, ∀ n ≥ N, |u n - l| ≤ ε

lemma ge_max_iff {α : Type*} [linear_order α] {p q r : α} :
r ≥ max p q  ↔ r ≥ p ∧ r ≥ q :=
max_le_iff

example (hu : seq_limit u l) (hv : seq_limit v l') :
seq_limit (u + v) (l + l') :=
begin
  intros eps eps_pos,
  cases hu (eps/2) (by linarith) with N1 hN1,
  cases hv (eps/2) (by linarith) with N2 hN2,
  let N0:= max N1 N2,
  use N0,
  intros n hn,
  rw ge_max_iff at hn,
  calc  |(u + v) n - (l + l')|
      = |u n + v n - (l + l')|   : rfl
  ... = |(u n - l) + (v n - l')| : by congr' 1 ; ring
  ... ≤ |u n - l| + |v n - l'|   : by apply abs_add
  ... ≤  eps                     : by linarith [hN1 n (by linarith),
                                                hN2 n (by linarith)],
end
\end{leancode}

En el caso de este resultado, se ha importado librería
\href{https://github.com/leanprover-community/mathlib/blob/master/src/data/real/basic.lean}{data.real.basic},
para trabajar con el conjunto de los números reales y sólo se ha usado
el siguiente lema auxiliar:
\begin{itemize}
\item \mint{lean}|abs_add : abs (a + b) ≤ abs a + abs b|
  \indLema{abs\_add}
\end{itemize}

Además del lema auxiliar, se han usado las tácticas
\tactica{rw /rewrite}{rw},
\tactica{use}{use},
\tactica{let}{let},
\tactica{apply}{apply},
\tactica{ring}{ring},
\tactica{congr'}{congr'},
\tactica{intro / intros}{intros},
\tactica{cases}{cases} y
\tactica{linarith}{linarith}.

\subsection{Paridad de la suma de funciones}

Como ya se adelantó, los tres resultados que vamos a estudiar a
continuación son relativos a la paridad de las funciones. Es por eso,
que previo al desarrollo de los resultados veamos las dos siguientes
definiciones:

\begin{definicion}\label{funpar}
  Sea \(f: ℝ → ℝ\), se dirá que \(f\) es una \textbf{función par} si
  para cualquier número real \(x\) se verifica que \(f(-x)=f(x)\).
\end{definicion}

\begin{definicion}\label{funimpar}
  Sea \(f: ℝ → ℝ\), se dirá que \(f\) es una \textbf{función impar} si
  para cualquier número real \(x\) se verifica que \(f(-x)=-f(x)\).
\end{definicion}

Estas dos definiciones se formalizan en Lean como sigue:
\begin{leancode}
def even_fun (f : ℝ → ℝ) := ∀ x, f (-x) = f x
def odd_fun  (f : ℝ → ℝ) := ∀ x, f (-x) = -f x
\end{leancode}

A continuación, ya estamos preparados para ver la demostración del
resultado deseado.

\begin{teorema}
  Sean \(f\) y \(g\) dos funciones pares, entonces
  se tiene que la suma de ambas, \((f+g)\), es también una función par.
\end{teorema}

\begin{demostracion}
  Por la definición \ref{funpar} de una función par, se tiene que se
  verifica lo siguiente:
  \begin{align}
    & ∀ x ∈ ℝ, f(-x)=f(x)  \label{fpar} \\
    & ∀ x ∈ ℝ, g(-x)=g(x). \label{gpar}
  \end{align}
  Como queremos demostrar que la suma de las funciones \(f\) y \(g\) es
  una función par, se tendría que probar que
  \begin{equation}
    ∀ x ∈ ℝ, (f+g)(-x) = (f+g)(x).
  \end{equation}
  Para probarlo, comencemos considerando un número real \(x\)
  arbitrario, entonces estudiemos:
  \[\begin{array}{llll}
      (f+g)(-x) &= &f(-x)+g(-x) & [(1)]\\
                &≤ &f(x)+g(x)   & [(2)] \\
                &≤ &(f+g)(x),   & [(3)]
    \end{array}\]
  donde tanto en el paso \((1)\) como en el \((3)\) hemos usado la
  propia definición de la suma de funciones, mientras que en \((2) \) se
  han usado las hipótesis de paridad (\ref{fpar}) y (\ref{gpar}). Por
  tanto, ya tendríamos el resultado deseado.
\end{demostracion}

Esta demostración se formaliza en Lean de la siguiente manera:
\begin{leancode}
import data.real.basic

def even_fun (f : ℝ → ℝ) := ∀ x, f (-x) = f x

example
  (f g : ℝ → ℝ)
  : even_fun f → even_fun g →  even_fun (f + g) :=
begin
  intros hf hg,
  intros x,
  calc (f + g) (-x)
      = f (-x) + g (-x) : rfl
  ... = f x + g (-x)    : by rw hf
  ... = f x + g x       : by rw hg
  ... = (f + g) x       : rfl
end
\end{leancode}

Como en el resto de ejemplos anteriores, para formalizar el resultado ha
sido necesario importar la librería
\href{https://github.com/leanprover-community/mathlib/blob/master/src/data/real/basic.lean}{data.real.basic}
para trabajar con el conjunto de los números reales.

Para llevar a cabo la formalización en Lean de este resultado no ha sido
necesario utilizar ningún lema ni teorema auxiliar. No obstante, sí ha
sido necesario usar las tácticas
\tactica{intro / intros}{intros},
\tactica{intro / intros}{intro},
\tactica{rw / rewrite}{rw},
\tactica{calc}{calc} y
\tactica{refl / reflexivity}{rfl}.

\subsection{Paridad de la composición de funciones}

\begin{teorema}
  Sea \(f\) una función par y sea \(g\) una función arbitraria, entonces
  se tiene que la composición de \(g\) con \(f\); es decir, \((g ∘ f)\)
  es también par.
\end{teorema}

\begin{demostracion}
  Al igual que en la demostración anterior, por la definición
  \ref{funpar}, se tiene que para la función \(f\) se verifica que
  \begin{equation}\label{fpar2}
     ∀ x ∈ ℝ, f(-x) = f(x).
  \end{equation}
  Consideremos ahora un número real \(x\) arbitrario y estudiemos la
  composición de una función arbitraria \(g\) con \(f\):
  \[\begin{array}{llll}
      (g ∘ f)(-x) &= &g(f(-x))    & [(1)]\\
                  &≤ &g(f(x))     & [(2)] \\
                  &≤ &(g ∘ f)(x), & [(3)]
    \end{array}\]
  donde en \((1)\) y \((3)\) se ha usado la definición de la
  composición; mientras que en \((2)\) se ha hecho uso de la hipótesis
  de paridad de \(f\), (\ref{fpar2}).
\end{demostracion}

En Lean esto se formalizaría como sigue:
\begin{leancode}
import data.real.basic

def even_fun (f : ℝ → ℝ) := ∀ x, f (-x) = f x

example (f g : ℝ → ℝ) : even_fun f → even_fun (g ∘ f) :=
begin
  intros hf x,
  calc (g ∘ f) (-x)
      = g (f (-x)) : rfl
  ... = g (f x)    : by rw hf,
end
\end{leancode}

En el ejemplo se ha importado la librería  \href{https://github.com/leanprover-community/mathlib/blob/master/src/data/real/basic.lean}{data.real.basic}
para trabajar con el conjunto de los números reales.

Al igual que en el caso anterior, para formalziar este resultado no ha
sido necesario el uso de ningún lema o teorema auxiliar; pero sí se han
usado las tácticas
\tactica{intro / intros}{intros}.
\tactica{rw / rewrite}{rw},
\tactica{calc}{calc} y
\tactica{refl / reflexivity}{rfl}.


\subsection{Imparidad de la composición de funciones impares}

\begin{teorema}
  Sean \(f\) y \(g\) dos funciones impares, entonces se tiene que la
  composición de ambas, \((g ∘ f)\), es también una función impar.
\end{teorema}

\begin{demostracion}
  Por la definición \ref{funimpar} de una función impar vista
  anteriormente, se tiene que se verifica lo siguiente:
  \begin{align}
    & ∀ x ∈ ℝ, f(-x) = -f(x)  \label{fimpar} \\
    & ∀ x ∈ ℝ, g(-x) = -g(x). \label{gimpar}
  \end{align}
  A continuación, vamos a estudiar la composición de estas dos funciones
  para un número real \(x\) arbitrario:
  \[\begin{array}{llll}
      (g ∘ f)(-x) &= &g(f(-x))     & [(1)]\\
                  &≤ &g(-f(x))     & [(2)] \\
                  &≤ &-(g ∘ f)(x), & [(3)]
    \end{array}\]
  donde en \((2)\) y en \((3)\) se han usado las hipótesis de funciones
  impares, (\ref{fimpar}) y (\ref{gimpar}) respectivamente.

  De esta forma ya tendríamos la demostración deseada.
\end{demostracion}

Prosigamos con la formalización en Lean de esta demostración:
\begin{leancode}
import data.real.basic

def odd_fun (f : ℝ → ℝ) := ∀ x, f (-x) = -f x

example
  (f g : ℝ → ℝ)
  : odd_fun f → odd_fun g →  odd_fun (g ∘ f) :=
begin
  intros hf hg x,
  calc (g ∘ f) (-x)
      =  g (f (-x)) : rfl
  ... =  g (-f x)   : by rw hf
  ... = -g (f x)    : by rw hg
  ... =  -(g ∘ f) x : rfl,
end
\end{leancode}

Para llevar a cabo la formalización de este resultado ha sido necesario
utilizar las tácticas
\tactica{intro / intros}{intros}.
\tactica{rw / rewrite}{rw},
\tactica{calc}{calc} y
\tactica{refl / reflexivity}{rfl}.

Asimismo, ha sido necesario importar la librería
\href{https://github.com/leanprover-community/mathlib/blob/master/src/data/real/basic.lean}{data.real.basic}
para trabajar con el conjunto de los números reales.

\section{Álgebra básica}

En esta segunda sección se estudiarán diversos resultados
correspondientes a la asignatura de ``Álgebra Básica''.  Al igual que
Cálculo Infinitesimal, también es del primer año del grado en
Matemáticas.

Se van a estudiar dos resultados para los cuales es necesario introducir
previamente el siguiente concepto:
\begin{definicion}\label{division}
  Denotaremos la divisibilidad en el conjunto de los números enteros por
  el símbolo \("|"\). Se dirá que el número entero \(a\) es un
  \textbf{divisor de} \(b\) (o que \(a\) \textbf{divide a} \(b\)) si y
  solamente si existe otro número entero \(k\) tal que \(b = a · k\).
  Simbólicamente,
  \begin{equation}
    a|b ⟺ ∃ k ∈ ℤ, b = a · k.
  \end{equation}
\end{definicion}

A continuación, una vez hemos introducido el concepto, ya estamos listos
para ver los resultados en cuestión.

\subsection{Transitividad de la división}

\begin{teorema}
  Sean \(a, b\) y \(c\) tres número enteros tales que verifican que
  \(a\) divide a \(b\) y \(b\) divide a \(c\), entonces se tiene que
  \(a\) divide a \(c\).
\end{teorema}

\begin{demostracion}
  Por la definición \ref{division} de división en el conjunto de los
  números enteros vista anteriormente, como consecuencia de que \(a\)
  divide a \(b\), se tiene que existe un \(k_1 ∈ ℤ\) tal que
  \begin{equation}\label{div1}
    b = a · k_1.
  \end{equation}
  Asimismo, como consecuencia de que \(b\) divide a \(c\) se verifica
  que existe un \(k_2 ∈ ℤ\) tal que
  \begin{equation}\label{div2}
    c = b · k_2.
  \end{equation}
  De manera que si introducimos la descomposición de \(b\) descrita en
  (\ref{div1}) en la de \(c\) vista en (\ref{div2}), se tiene que:
  \[\begin{array}{ll}
      c & = (a · k_1) · k_2\\
        & = a · (k_1 · k_2) \\
        & = a · k_3,
    \end{array}\]
  donde \(k_3 = k_1 · k_2\). Y ya tendríamos el resultado deseado.
\end{demostracion}

Veamos ahora la formalización en Lean:
\begin{leancode}
import data.int.parity

variables (a b c : ℤ)

example (h₁ : a ∣ b) (h₂ : b ∣ c) : a ∣ c :=
begin
  cases h₁ with k1 hk1,
  cases h₂ with k2 hk2,
  rw hk1 at hk2,
  use k1*k2,
  rw ← mul_assoc,
exact hk2,
end
\end{leancode}

Para la formalización del resultado se ha importado la librería
\href{https://github.com/leanprover-community/mathlib/blob/master/src/data/int/basic.lean}{data.int.basic}
que nos permite trabajar con el conjunto de los números enteros.

En la formalización de este resultado ha sido necesario usar el siguiente
lema auxiliar:
\begin{itemize}
\item \mint{lean}|mul_assoc : a * b * c = a * (b * c)|
  \indLema{mul\_assoc}
\end{itemize}

Aparte del lema auxiliar, se han usado las tácticas
\tactica{cases}{cases},
\tactica{rw /rewrite}{rw},
\tactica{use}{use} y
\tactica{exact}{exact},

\subsection{Aditividad de la división}

\begin{teorema}
  Sean \(a, b\) y \(c\) tres números enteros tales que verifican que
  \(a\) divide a \(b\) y a \(c\), entonces se tiene que \(a\) divide a
  la suma de \(b\) y \(c\).  Simbólicamente:
  \begin{equation}
  a|b, \ a|c \ ⟹ \ a|(b+c).
  \end{equation}
\end{teorema}

\begin{demostracion}
  Análogamente a la demostración anterior, por la definición
  \ref{division}, se tiene que existen \(k_1, k_2 ∈ ℤ\) tales que
  \begin{align}
    & b = a · k_1 \label{div3} \\
    & c = a · k_2 . \label{div4}
  \end{align}
  De manera que sumando las dos expresiones descritas en las expresiones
  (\ref{div3}) y (\ref{div4}), se llega a que:
  \begin{equation*}
    b+c = a· (k_1+k_2).
  \end{equation*}
  Teniendo en cuenta la definición \ref{division}, ya se tendría
  el resultado.
\end{demostracion}

La formalización de esta demostración en Lean sería:
\begin{leancode}
import data.int.parity

variables (a b c : ℤ)

example
  (h1 : a ∣ b)
  (h2 : a ∣ c)
  : a ∣ b+c :=
begin
  cases h1 with k1 hk1,
  rw hk1,
  cases h2 with k2 hk2,
  rw hk2,
  use k1+k2,
  ring,
end
\end{leancode}

En la formalización de este resultado ha sido necesario importar dos librerías:
\begin{itemize}
\item \href{https://github.com/leanprover-community/mathlib/blob/master/src/data/int/basic.lean}{data.int.basic},
  para trabajar con el conjunto de los números enteros.

\item \href{https://github.com/leanprover-community/mathlib/tree/master/src/tactic}{tactic},
  para poder hacer uso de algunas tácticas como \tactica{ring}{ring}.
\end{itemize}

Asimismo, aparte de la táctica \tactica{ring}{ring}, ha sido necesario
el uso de las tácticas
\tactica{cases}{cases},
\tactica{rw /rewrite}{rw} y
\tactica{use}{use}.

%%% Local Variables:
%%% mode: latex
%%% TeX-master: "IMO_en_Lean"
%%% End:
